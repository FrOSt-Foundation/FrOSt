; *************************
; Project: FrOSt
; File: deleteFile.dasm
; *************************

; =======================
; FFAT16_deleteFile
; Arguments :
; -----------
; B : Pointeur vers le nom de fichier.
; Retourne :
; ----------
; EX : 0 si pas d'erreur, sinon code d'erreur
; =======================

:FFAT16_deleteFile
	set EX, 0

	ife 0, [FFAT16_valid]
		set EX, 0x0001
	ife EX, 0x0001
		set PC, POP

	jsr push_regs

	set B, Y
	jsr FFAT16_getFileByName ;Ce qui nous intéresse, c'est le secteur X (Xe entrée FAT)
	ifn EX, 0
		set PC, FFAT16_deleteFile_break0

	set PUSH, X
		set X, 1
		jsr FFAT16_readSector
		jsr FFAT16_waitFinish
	set X, POP
	;FAT enregistrée en *Z
	set C, X
	sub X, 1
	mul X, 16
	add X, Z ;X est désormais l'adresse de l'entrée FAT recherchée
	jsr FFAT16_getFilesNumber ;Y est le nombre de fichiers

	; Désormais :
	; C = Cème entrée FAT
	; X = Adresse de l'entrée FAT
	; Y = Nombre de fichiers
	; Z = Adresse de la FAT (pour le free)
	ifn C, Y ;Si on a supprimé le dernier fichier, on ne veut pas déplacer la dernière entrée FAT à sa place
		jsr FFAT16_deleteFile_moveLastFATEntry
	jsr FFAT16_deleteFile_eraseLastFATEntry ;On supprime la dernière entrée, car dans les deux cas elle est de trop (copiée ailleurs ou c'est ce qu'on veut supprimer)

	set EX, 0
	set X, 1
	set Y, Z
	jsr FFAT16_writeSector
	ifn EX, 0
		set PC, FFAT16_deleteFile_error0
	jsr FFAT16_waitFinish

	set B, Z
	int INT_FREE

	:FFAT16_deleteFile_break0
	jsr pop_regs

	set PC, POP

:FFAT16_deleteFile_error0
	set B, Z
	int INT_FREE
	set PC, FFAT16_deleteFile_break0

:FFAT16_deleteFile_moveLastFATEntry
	set PUSH, X
	jsr FFAT16_findFirstFreeFATEntry
	sub X, 16 ;On trouve la première entrée FAT libre et on lui soutire 16 : C'est la dernière entrée.
	;On la copie vers notre emplacement
	set B, X
	set A, POP
	set C, 14
	jsr memcpy

	set PC, POP

:FFAT16_deleteFile_eraseLastFATEntry
	set PUSH, X
	jsr FFAT16_findFirstFreeFATEntry
	sub X, 16 ;On trouve la première entrée FAT libre et on lui soutire 16 : C'est la dernière entrée.
	set I, 0
	:FFAT16_deleteFile_loop0 ;On efface le contenu de l'entrée FAT
		sti [X], 0
		add X, 1
		ifn I, 16
			set PC, FFAT16_deleteFile_loop0
	set X, POP

	set PC, POP
