; *************************
; Project: FrOSt
; File: init.dasm
; *************************

; ===================
; Error codes (en Z)
; ===================
; 0x0000 = Pas de problème
; 0x0001 = Pas de lecteur
; 0x0002 = Pas de floppy
; 0x0003 = Erreur de malloc
; 0x0004 = Erreur de lecture
; 0x0005 = La chaine d'identification n'est pas correcte (!FFAT16)

:FFAT16_init
	;Checking disk drive
	ife [id_floppy1], 0 ;Si il n'y a pas de lecteur de floppy
		set EX, 0x0001
	ife [id_floppy1], 0 ;Si il n'y a pas de lecteur de floppy
		set PC, POP
	
	set A, 0
	hwi [id_floppy1] ;Si il n'y a pas de floppy dans le lecteur
	ife B, 0x0
		set EX, 0x0002
	ife B, 0x0
		set PC, POP
		
	;On alloue 512 (un secteur) mots en RAM dans lesquels on lira le premier secteur
	set B, 512
	int INT_MALLOC
	
	ife Z, MALLOC_ERROR ;Si impossible d'allouer de la mémoire
		set EX, 0x0003
	ife Z, MALLOC_ERROR ;Si impossible d'allouer de la mémoire
		set PC, POP
	
	;On lit à l'adresse retournée par MALLOC
	set A, 2
	set X, 0
	set Y, Z
	hwi [id_floppy1]
	ifn B, 1 ;Si erreur
		set EX, 0x0004
	ifn B, 1 ;Si erreur
		set PC, POP
		
	jsr FFAT16_waitFinish
	
	;Vérification de la présence de 0xAAAA et 0xBBBB (identifie comme FFAT-16 compatible)
	set I, Z
	
	ifn [I], 0xFF16
		jsr FFAT16_notCompatible
	add I, 1
	
	ifn [I], 0xFF16
		jsr FFAT16_notCompatible
	
	
	;Enregistrement du numéro de série
	add I, 1
	set [serialNumber], [I]
	
	add I, 1
	set [serialNumber2], [I]
	
	add I, 1
	
	set B, I
	set A, volumeName
	set C, 15
	jsr memcpy
	
	set [FFAT16_valid], 1
	
	set B, Z
	int INT_FREE
	
	set PC, POP
	
:FFAT16_notCompatible
	set B, Z
	int INT_FREE
	
	set EX, 0x0005
	
	set A, POP
	
	set PC, POP
