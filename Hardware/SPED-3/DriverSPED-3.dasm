;*************************
; Project: FrOSt
; File: DriverSPED.dasm
;*************************

.define SPED3_STATE_NO_DATA   0
.define SPED3_STATE_RUNNING   1
.define SPED3_STATE_TURNING   2

.define SPED3_ERROR_NONE     0
.define SPED3_ERROR_BROKEN   0xffff

:sped3_buffer_begin		dat 0x0000
:sped3_buffer_capacity	dat 0x0000

:sped3_angle				dat 0x0000

;-----------------------------------------------;
;					Sorties						;
;-----------------------------------------------;

; Interroge le SPED-3 sur son état.
; return B = état actuel
; return C = dernière erreur
:sped3_pollState
	set PUSH, A

	set A, 0
	hwi [id_sped]

	set A, POP
	set PC, POP

; Attend jusqu'à la fin de la rotation en cour
:sped3_waitForStaticState
	set PUSH, A
	set A, 0
	:sped3_waitForStaticState_loop1
		hwi [id_sped]
		ife B, 2
			set PC, sped3_waitForStaticState_loop1
	set A, POP
	set PC, POP

;-----------------------------------------------;
;				Configuration					;
;-----------------------------------------------;

; Définit les variables du buffer du SPED-3
; X = Adresse de début du buffer
; Y = Adresse de fin du buffer
:sped3_setBuffer_data
	set PUSH, Y
	
	sub Y, X
	div Y, 2
	set sped3_buffer_begin, X
	set sped3_buffer_capacity, Y
	
	set Y, POP
	set PC, POP 

; Définit les variables du buffer du SPED-3
; X = Adresse de début du buffer
; Y = Nombre de points contenu dans le buffer
:sped3_setBuffer_point
	set sped3_buffer_begin, X
	set sped3_buffer_capacity, Y
	
	set PC, POP


;-----------------------------------------------;
;					Commandes					;
;-----------------------------------------------;


; Remet le SPED dans son état à l'allumage.
:sped3_reinit
	set PUSH, A
	set PUSH, B
	set PUSH, C
	set PUSH, X
	set PUSH, Y

	; On réinitialise l'angle du sped à 0
	set A, 2
	set X, 0
	hwi [id_sped]
	
	set sped3_angle, 0

	; On attend que la rotation se termine.
	set A, 0
	:sped3_reinit_loop1
		hwi [id_sped]
		ife B, 2
			set PC, sped3_reinit_loop1

	; On affiche rien
	set A, 1
	set Y, 0
	hwi [id_sped]
	
	set sped3_buffer_begin, 0
	set sped3_buffer_capacity, 0

	set Y, POP
	set X, POP
	set C, POP
	set B, POP
	set A, POP
	set PC, POP

; Met à jour le SPED selon les variables
; X = Paramètre de sécurité, voir sped3_rotate
:sped3_update
	set PUSH, A
	set PUSH, X
	set PUSH, Y
	
	set Y, X
	set X, sped3_angle
	jsr sped3_rotate
	
	set A, 1
	set X, sped3_buffer_begin
	set Y, sped3_buffer_capacity
	hwi [id_sped]
	
	set Y, POP
	set X, POP
	set A, POP
	set PC, POP

; Affiche le buffer
:sped3_printBuffer
	set PUSH, A
	set PUSH, X
	set PUSH, Y
	
	set A, 1
	set X, sped3_buffer_begin
	set Y, sped3_buffer_capacity
	hwi [id_sped]
	
	set Y, POP
	set X, POP
	set A, POP
	set PC, POP

; Efface l'écran du SPED-3.
:sped3_clear
	set PUSH, A
	set PUSH, Y

	set A, 1
	set Y, 0
	hwi [id_sped]

	set Y, POP
	set A, POP
	set PC, POP

; Tourne l'affichage
; X = Nombre de degrés de la rotation
; Y = Paramètre de sécurité, 0: passer si déjà en train de tourner, 1: attente début, 2: attente fin, 3: attente debut et fin
.define SPED3_ROTATE_SKIP_IF_TURNING		0
.define SPED3_ROTATE_WAIT_BEGIN			1
.define SPED3_ROTATE_WAIT_END			2
.define SPED3_ROTATE_WAIT_BOTH			3

:sped3_rotate
	set PUSH, A
	set PUSH, B

	set A, 0
	ifn Y, 0
		set PC, sped3_rotate_skip1
		
		hwi [id_sped]
		ife B, 2
			set PC, sped3_rotate_skip3
		set PC, sped3_rotate_skip2
		
	:sped3_rotate_skip1
	
	ife Y, 2
		set PC, sped3_rotate_skip2

		:sped3_rotate_loop1
			hwi [id_sped]
			ife B, 2
				set PC, sped3_rotate_loop1

	:sped3_rotate_skip2
	
	set A, 2
	hwi [id_sped]
	
	set sped3_angle, X
	
	ifl Y, 2
		set PC, sped3_rotate_skip3

	set A, 0
	:sped3_rotate_loop2
		hwi [id_sped]
		ife B, 2
			set PC, sped3_rotate_loop2

	:sped3_rotate_skip3
	
	set B, POP
	set A, POP
	set PC, POP

; Même chose que la fonction précédente, mais ne vérifie pas les paramètres et
; n'attend pas que la rotation soit effectuée.
:sped3_rotate_unsafe
	set PUSH, A

	set A, 2
	hwi [id_sped]
	
	set sped3_angle, X

	set A, POP
	set PC, POP
