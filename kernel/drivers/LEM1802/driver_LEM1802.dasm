; --------------
;  lem1802_init
; --------------
; Description:
;     - Inits the LEM1802. Used by the hardware manager.
; Arguments:
;     - J: Device ID
;     - (Not used) Y: Device version
;     - (Not used) Z: Device data location
; Returns:
;     -

:lem1802_init
	set PC, POP

; --------------
;  lem1802_resetFont
; --------------
; Description:
;     - Resets the font ram.
; Arguments:
;     - J: Device ID
;     - Z: Device data location
; Returns:
;     -
:lem1802_resetFont
	set PUSH, A
	set PUSH, B

	set A, 1
	set B, lem1802_font_default
	hwi [J]

	set [Z], 0xF000

	set B, POP
	set A, POP
	set PC, POP

; -----------------
;  lem1802_setFont
; -----------------
; Description:
;     - Maps the font ram.
; Arguments:
;     - B: Pointer to the font ram to map
;     - J: Device ID
;     - (Not used) Z: Device data location
; Returns:
;     -

:lem1802_setFont
	set PUSH, A

	set A, 1
	hwi [J]

	set A, POP
	set PC, POP

; --------------
;  lem1802_setColor
; --------------
; Description:
;     - Sets the color of the characters to write.
; Arguments:
;     - B: Color (unchecked by this routine)
;     - (Not used) J: Device ID
;     - Z: Device data location
; Returns:
;     -

:lem1802_setColor
	set [Z], B

	set PC, POP

; --------------
;  lem1802_getColor
; --------------
; Description:
;     - Returns the color of the characters to write.
; Arguments:
;     - (Not used) J: Device ID
;     - Z: Device data location
; Returns:
;     - B: Color

:lem1802_getColor
	set B, [Z]

	set PC, POP

; --------------
;  lem1802_setPalette
; --------------
; Description:
;     - Maps the palette RAM
; Arguments:
;     - B: Pointer to the palette ram to map, or 0 to revert to the default palette.
;     - J: Device ID
;     - (Not used) Z: Device data location
; Returns:
;     -

:lem1802_setPalette
	set PUSH, A

	set A, 2
	hwi [J]

	set A, POP
	set PC, POP

; --------------
;  lem1802_setBorder
; --------------
; Description:
;     - Sets the color of the border.
; Arguments:
;     - B: Color (0x0000 - 0x000F)
;     - J: Device ID
;     - (Not used) Z: Device data location
; Returns:
;     -

:lem1802_setBorder
	set PUSH, A

	set A, 3
	hwi [J]

	set A, POP
	set PC, POP

; --------------
;  lem1802_setVram
; --------------
; Description:
;     - Maps the VRAM.
; Arguments:
;     - B: Pointer to VRAM
;     - (Not used) J: Device ID
;     - (Not used) Z: Device data location
; Returns:
;     -

:lem1802_setVram
	set [active_screen], B

	set PC, lem1802_updateVram

; --------------
;  lem1802_getFont
; --------------
; Description:
;     - Copies the font to a new allocated RAM block.
; Arguments:
;     - J: Device ID
;     - (Not used) Z: Device data location
; Returns:
;     - Z: The address of the RAM block containing the font, or MALLOC_ERROR (use  ife Z, MALLOC_ERROR to check)

:lem1802_getFont
	set PUSH, A

	set A, 256
	jsr malloc
	 ife Z, MALLOC_ERROR
		set PC, lem1802_getFont_end

	set A, 4
	set B, Z
	hwi [J]
	:lem1802_getFont_end

	set A, POP
	set PC, POP

; --------------
;  lem1802_getPalette
; --------------
; Description:
;     - Copies the palette to a new allocated RAM block.
; Arguments:
;     - J: Device ID
;     - (Not used) Z: Device data location
; Returns:
;     - Z: The address of the RAM block containing the font, or MALLOC_ERROR (use  ife Z, MALLOC_ERROR to check)

:lem1802_getPalette
	set PUSH,A

	set A, 16
	jsr malloc
	 ife Z, MALLOC_ERROR
		set PC, lem1802_getPalette_end

	set A, 5
	set B, Z
	hwi [J]
	:lem1802_getPalette_end

	set A, POP
	set PC, POP

; --------------
;  lem1802_getVram
; --------------
; Description:
;     - Get the VRAM location.
; Arguments:
;     - (Not used) J: Device ID
;     - (Not used) Z: Device data location
; Returns:
;     - Z: Pointer to VRAM

:lem1802_getVram
	set Z, [active_screen]

	set PC, POP

; --------------
;  INTERNAL ROUTINE - DO NOT USE
; --------------

:lem1802_updateVram
	set PUSH, A
	set PUSH, B

	set A, 0
	set B, [active_screen]
	hwi [J]

	set B, POP
	set A, POP
	set PC, POP

; --------------
;  lem1802_displayFrOStLogo
; --------------
; Description:
;     - Displays the FrOSt logo.
; Arguments:
;     -
; Returns:
;     -

:lem1802_displayFrOStLogo
	set PUSH, A
	set PUSH, B
	set PUSH, I
	set PUSH, J

	jsr lem1802_clear
	set B, lem1802_FrOStLogo_font
	jsr lem1802_setFont

	set B, 3
	jsr lem1802_setBorder

	set I, lem1802_FrOStLogo
	set J, [active_screen]
	add J, 137
	set A, 0
	:lem1802_displayFrOStLogo_loop1
		sti [J], [I]
		add A, 1
		ife A, 16
			set A, 0
		ifn I, lem1802_FrOStLogo_end
			set PC, lem1802_displayFrOStLogo_loop1

	; Wait for a while before making the logo disappear
	set A, 0
	:lem1802_displayFrOStLogo_loop2
		add A, 1
		ifn A, 0xFFFF ;TODO: Tweak it when we know the monitor startup time
			set PC, lem1802_displayFrOStLogo_loop2

	set B, 0
	jsr lem1802_setFont
	jsr lem1802_setBorder

	set J, POP
	set I, POP
	set B, POP
	set A, POP
	set PC, POP

; --------------
;  lem1802_clearLine
; --------------
; Description:
;     - Clears line on screen.
; Arguments:
;     - I: Line to clear (if I > 12, then clearing line 12).
; Returns:
;     -

:lem1802_clearLine
	set PUSH, I
	set PUSH, J

	ifg I, lem1802_HEIGHT
		set I, lem1802_HEIGHT
	sub I, 1
	mul I, lem1802_WIDTH
	add I, [active_screen]
	set J, 0
	:lem1802_clearLine_loop0
		sti [I],0
		ifn J, lem1802_WIDTH
			set PC, lem1802_clearLine_loop0

	set J, POP
	set I, POP
	set PC, POP

; --------------
;  lem1802_scroll
; --------------
; Description:
;     - Everything goes up by one line, clears the last line.
; Arguments:
;     - (Not used) J: Device ID
;     - (Not used) Z: Device data location
; Returns:
;     -
:lem1802_scroll
	set PUSH, A
	set PUSH, B
	set PUSH, C
	set PUSH, I

	set A, [active_screen]
	set B, A
	add B, lem1802_WIDTH
	set C, lem1802_SIZE
	sub C, lem1802_WIDTH
	jsr memcpy
	set I, 12
	jsr lem1802_clearLine

	set I, POP
	set C, POP
	set B, POP
	set A, POP
	set PC, POP

; --------------
;  lem1802_printf
; --------------
; Description:
;     - Writes a formatted string to the screen.
; Arguments:
;     - A: Pointer to null-terminated string.
;     - I: Row (1 < I < 12).
;     - J: Column (1 < J < 32).
; Returns:
;     -

:lem1802_printf
	set PUSH, B
	set PUSH, X
	set PUSH, Y
	set PUSH, I
	set PUSH, J

	jsr lem1802_getColor
	set PUSH, B

	ife [A], 0
		set PC, lem1802_printf_end
	ifl I, 1
		set I, 1
	ifg I, lem1802_HEIGHT
		set I, lem1802_HEIGHT
	ifl J, 1
		set J, 1
	ifg J, lem1802_WIDTH
		set J, lem1802_WIDTH
	sub I, 1
	sub J, 1
	mul I, lem1802_WIDTH
	add I, J
	add I, [active_screen]
	set J, A
	set Y, lem1802_SIZE
	add Y, [active_screen]
	:lem1802_printf_loop0
		set B, [J]
		ife I, Y
			jsr lem1802_printf_scroll
		ifl B, 0x20
			set PC, lem1802_printf_selection
		bor B, PEEK
		sti [I], B
		ifn [J], 0
			set PC, lem1802_printf_loop0

	:lem1802_printf_end
	set B, POP
	set J, POP
	set I, POP
	set Y, POP
	set X, POP
	set B, POP
	set pc, POP

:lem1802_printf_selection
	ife B, 0x11
		set PC, lem1802_printf_return
	ife B, 0x1F
		set PC, lem1802_printf_tab
	add J, 1
	set PC, lem1802_printf_loop0

:lem1802_printf_scroll
	jsr lem1802_scroll
	sub I, lem1802_WIDTH
	set PC, POP

:lem1802_printf_return
	set X, I
	sub X, [active_screen]
	mod X, lem1802_WIDTH
	add I, lem1802_WIDTH
	sub I, X
	add J, 1
	ifl I, Y
		ifn I, Y
			set PC, lem1802_printf_loop0
	jsr lem1802_scroll
	sub I, lem1802_WIDTH

	set PC, lem1802_printf_loop0

:lem1802_printf_tab
	add I, 3
	add J, 1
	ifl I, Y
		ifn I, Y
			set PC, lem1802_printf_loop0
	jsr lem1802_scroll
	sub I, lem1802_WIDTH

	set PC, lem1802_printf_loop0

; --------------
;  lem1802_clear
; --------------
; Description:
;     - Clears the screen.
; Arguments:
;     -
; Returns:
;     -

:lem1802_clear
	set PUSH, A

	set A, [active_screen]
	jsr clear_memory

	set A, POP
	set PC, POP

; --------------
;  lem1802_printc
; --------------
; Description:
;     - Writes a char.
; Arguments:
;     - C: char
;     - I: Line (1 < I < 12)
;     - J: Column (1 < J < 32)
; Returns:
;     -

:lem1802_printc
	set PUSH, I
	set PUSH, J
	set PUSH, C

	ifl I, 1
		set I, 1
	ifg I, lem1802_HEIGHT
		set I, lem1802_HEIGHT
	ifl J, 1
		set J, 1
	ifg J, lem1802_WIDTH
		set J, lem1802_WIDTH
	sub I, 1
	sub J, 1
	mul I, lem1802_WIDTH
	add I, J
	add I, [active_screen]
	bor C, [lem1802_color]
	set [I], C

	set C, POP
	set J, POP
	set I, POP
	set PC, POP

; --------------
;  lem1802_init
; --------------
; Description:
;     - Quickly writes an unformatted string.
; Arguments:
;     - B: Pointer to the string
;     - I: Pointer to VRAM address to write to
; Returns:
;     -

:lem1802_puts
	set PUSH, C
	set PUSH, I
	set PUSH, J

	set C, [lem1802_color]
	add I, [active_screen]
	set J, B
	:lem1802_puts_loop0
		bor [J], C
		sti [I], [J]
		ifn [J], 0
			set PC, lem1802_puts_loop0

	set J, POP
	set I, POP
	set C, POP
	set PC, POP
