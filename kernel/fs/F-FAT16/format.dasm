; --------------
;  FFAT16_format
; --------------
; Description:
;     - Formats the disk to FFAT-16.
; Arguments:
;     -
; Returns:
;     - Z: 0 if no error, else error code

; ===============
; Error codes
; ===============
; 0x0001 : Malloc error
; 0x0002 : No lector
; 0x0003 : No floppy
; 0x0004 : Error while writing

:FFAT16_format
	set PUSH, Y
	set PUSH, A
	set PUSH, B
	set PUSH, C

	set B, 512
	int INT_MALLOC ; We allocated a memory block, that we will fill then write on the disk
	ife Z, MALLOC_ERROR
		set Y, 0x0001
	ife Z, MALLOC_ERROR
		set PC, FFAT16_format_error

	set [0xFFB0], Z

	; We copy the default content (cf ./DAT.dasm) to the previously allocated memory block
	set A, defaultContent
	set B, defaultContent_end
	set C, Z
	:FFAT16_format_loop0
		set [C], [A]
		add A, 1
		add C, 1
		ifn A, B
			set PC, FFAT16_format_loop0

	; If no disk present, error
	ife [id_floppy1], 0
		set Y, 0x0002
	ife [id_floppy1], 0
		set PC, FFAT16_format_error_freeMem

	; We check that the disk is in STATE_READY
	set A, 0
	HWI [id_floppy1]
	ifn B, 1
		set Y, 0x0003
	ifn B, 1
		set PC, FFAT16_format_error_freeMem


	; We write on the disk
	set X, 0
	set Y, Z
	set EX, 0
	jsr FFAT16_writeSector
	ifn EX, 0
		set PC, FFAT16_format_error_freeMem

	jsr FFAT16_waitFinish ; We wait until the write is finished

	; We free the previously allocated memory
	set B, Z
	int INT_FREE

	; We empty the FAT
	set B, 512
	int INT_MALLOC
	ife Z, MALLOC_ERROR
		set Y, 0x0001
	ife Z, 0x0001
		set PC, FFAT16_format_error

	set I, Z
	set J, Z
	add J, 512
	:FFAT16_format_loop1
		set [I], 0
		add I, 1
		ifn I, J
			set PC, FFAT16_format_loop1

	set X, 1
	set Y, Z
	JSR FFAT16_writeSector

	ifn EX, 0
		set Y, 0x0004
	ifn EX, 0
		set PC, FFAT16_format_error_freeMem

	set B, Z
	int INT_FREE

	set Z, 0x0000

	set C, POP
	set B, POP
	set A, POP
	set Y, POP

	set PC, POP

:FFAT16_format_error_freeMem ; Called when memory needs to be deallocated too
	set B, Z
	jsr INT_FREE
:FFAT16_format_error
	set C, POP
	set B, POP
	set A, POP

	set Z, Y
	set Y, POP

	set PC, POP
